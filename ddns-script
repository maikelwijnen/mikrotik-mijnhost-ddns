Copyright (c) 2025 Maikel Wijnen

# ==========================================
# Mijn.Host Dynamic DNS Updater (PATCH versie, meerdere subdomains)
# Lijst kan opgedeeld worden met ;
# subdomain format like {"SUBDOMAIN-A";"SUBDOMAIN-B";"SUBDOMAIN-C"}
# ==========================================

:local apiKey "API_SLEUTEL"
:local domain "DOMAIN.NAME"
:local subdomains {"SUBDOMAIN"}
:local ttl "900"


# --- Publiek IP ophalen ---
:global currentIP ([/tool fetch url="https://api.ipify.org" mode=https output=user as-value]->"data")

# --- Veiligheidscheck: geen IP of ongeldig IP ---
:if ([:len $currentIP] = 0 || [:find $currentIP "."] = nil) do={
    :log warning "[mijnhost-ddns] Ongeldig of leeg IP, update overgeslagen"
    :error "geen geldig ip"
}

# --- Alleen bij wijziging uitvoeren ---
:global mijnhostDnsrecordIP
:if ($currentIP != $mijnhostDnsrecordIP) do={

    :foreach subdomain in=$subdomains do={

        # JSON-payload voor elk subdomein
        :local json ("{\"record\":{\"type\":\"A\",\"name\":\"" . $subdomain . "." . $domain . ".\",\"value\":\"" . $currentIP . "\",\"ttl\":" . $ttl . "}}")

        # PATCH-request naar mijn.host API
        /tool fetch url=("https://mijn.host/api/v2/domains/" . $domain . "/dns") \
            http-header-field=("API-Key: " . $apiKey . "\r\nContent-Type: application/json") \
            http-method=patch http-data=$json mode=https output=none

        :log info ("[mijnhost-ddns] Record " . $subdomain . " bijgewerkt naar " . $currentIP)
    }

    # Nieuw IP opslaan na alle updates
    :global mijnhostDnsrecordIP $currentIP
}
